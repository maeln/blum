char = { ANY }

separator = { "----" ~ NEWLINE }

heading = { "#" ~ (!NEWLINE ~ char)+ ~ NEWLINE }

bold_text   = { "*" ~ (!"*" ~ char)* ~ "*" }
italic_text = { "/" ~ (!"/" ~ char)* ~ "/" }

link_name = { (!"]" ~ char)* }
link_url  = { (!")" ~ char)* }
link      = { "[" ~ link_name ~ "]" ~ "(" ~ link_url ~ ")" }

// Adjusted to include inline formatting within text blocks
text_block = { (!(NEWLINE) ~ (bold_text | italic_text | link | char))+ ~ NEWLINE? }
code_block = { ">>>" ~ NEWLINE ~ (!">>>" ~ (char | NEWLINE))+ ~ ">>>" ~ NEWLINE? }

sidenote = { "|" ~ (!"|" ~ (code_block | bold_text | italic_text | link | char))+ ~ "|" ~ NEWLINE? }

// The article part of the file
article = { (heading | sidenote | code_block | text_block | NEWLINE)+ }

// The metadata part of the file
key = { (!("=" | NEWLINE) ~ char)+ }
value = { (!NEWLINE ~ char)+ }
property = { key ~ "=" ~ value }
metadata = { (property ~ NEWLINE)* }


// Top rule to start parsing
document = { SOI ~ (metadata ~ separator)? ~ article ~ EOI }
